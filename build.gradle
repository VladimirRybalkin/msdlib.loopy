apply plugin: 'java'
apply plugin: 'eclipse'

version = '0.0.1'
description = 'some interesting description'

String libDir       = "$projectDir/lib"
String genLibDir    = "$projectDir/lib"
String specDir      = "$projectDir/specs"
String genDir       = "$projectDir/src-gen"
String parserGenDir = "$genDir/de/hopp/generator/parser"

String flexSpec     = "$specDir/mhs.flex";
String cupSpec      = "$specDir/mhs.cup"
String mhsSpec      = "$specDir/mhs.katja"
String boardSpec    = "$specDir/board.katja"
String modelSpec    = "$specDir/cmodel.katja"

String flexLib      = "$libDir/JFlex.jar"
String cupLib       = "$libDir/java-cup-11a.jar"
String katja        = "$libDir/katja.jar"
String katjaCommon  = "$genLibDir/katja.common.jar"

String mhs          = "$genLibDir/MHS.jar"
String board        = "$genLibDir/BoardSpec.jar"
String model        = "$genLibDir/Model.jar"

String cupParser    = "$parserGenDir/parser.java"
String flexScanner  = "$parserGenDir/Lexer.java"
String symTab       = "$parserGenDir/sym.java"

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'de.jflex', name: 'jflex', version: '1.4.3'
  // compile group: 'net.sf.squirrel-sql.thirdparty-non-maven', name: 'java-cup', version: '0.11a'
  compile files("$cupLib")
  compile files("$katjaCommon", "$model", "$board", "$mhs")
  testCompile group: 'junit', name: 'junit', version: '4.+'
}

sourceSets {
  main {
    java {
      srcDirs "src-gen"
    }
    resources {
      // srcDirs "deploy"
      // srcDir projectDir 
      // include 'deploy/'
      srcDirs 'src/main/resources' 
    }
  }
}

eclipse {
  classpath {
    defaultOutputDir = file('build-eclipse')
  }
}

// removes src-gen folder
task removeGen << {
  delete "$genDir"
  delete "$genLibDir"
}

// removes generated AST
task removeAST << {
  delete "$mhs"
}

// removes generated board model
task removeBoard << {
  delete "$board"
}

// removes generated c/c++ model and common files
task removeModel << {
  delete "$model", "$katjaCommon"
}

// builds the ast model used by the cup parser
task buildAST {
  inputs.files("$katja", "$mhsSpec")
  outputs.files("$mhs")
  doLast {
    ant.java(jar:"$katja",fork:true) {
      arg(value: "-b")
      arg(value: "java")
      arg(value: "-d")
      arg(value: "$genLibDir")
      arg(value: "-o")
      arg(value: "$mhsSpec")
    }
  }
}

// builds the ast of the board
task buildBoard {
  inputs.files("$katja", "$boardSpec")
  outputs.files("$board")
  doLast {
    ant.java(jar:"$katja",fork:true) {
      arg(value: "-b")
      arg(value: "java")
      arg(value: "-d")
      arg(value: "$genLibDir")
      arg(value: "-o")
      arg(value: "$boardSpec")
    }
  }
}

// builds the c/c++ model used by the generator
task buildModel {
  inputs.files("$katja", "$modelSpec")
  outputs.files("$katjaCommon", "$model")
  doLast {
    ant.java(jar:"$katja",fork:true) {
      arg(value: "-b")
      arg(value: "java")
      arg(value: "-d")
      arg(value: "$genLibDir")
      arg(value: "-o")
      arg(value: "-j")
      arg(value: "$modelSpec")
    }
  }
}

task runKatja

task runJFlex {
  inputs.files(flexSpec, flexLib)
  outputs.file(flexScanner)
  doLast {
    ant.taskdef name: 'jflex', classname: 'JFlex.anttask.JFlexTask', classpath: flexLib
    ant.jflex(file: flexSpec, outdir: parserGenDir)
  }
}

task runCup {
  inputs.files(cupSpec, cupLib)
  outputs.files(cupParser, symTab)
  doLast {
    ant.taskdef name: 'jcup', classname: 'java_cup.anttask.CUPTask', classpath: cupLib
    ant.jcup(srcfile: cupSpec, destdir: genDir)
// srcfile, destdir, interface = true/false, package
  }
}

task generateResources

clean.dependsOn removeGen
clean.dependsOn removeAST
clean.dependsOn removeBoard
clean.dependsOn removeModel

runKatja.dependsOn buildAST
runKatja.dependsOn buildBoard
runKatja.dependsOn buildModel

generateResources.dependsOn runKatja
generateResources.dependsOn runJFlex
generateResources.dependsOn runCup

compileJava.dependsOn generateResources

jar {
  // add main class to manifest
  manifest {
    attributes("Main-Class": "de.hopp.Main")
  }

  // the following line is responsible for packaging all compile dependencies as well
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

  // also add the deploy folder
  //from "deploy"
}
