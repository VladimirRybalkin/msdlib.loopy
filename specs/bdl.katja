specification BDL

backend java {
  package de.hopp.generator.frontend
  import java.lang.String
  import java.lang.Integer
}

root BDLFile Pos

external String
external Integer

BDLFile (Imports imports, Backends backends, Options opts, Cores cores, GPIOs gpios, Instances insts, Medium medium, Scheduler scheduler)

Decl = Import | Backend | Option | Core | GPIO | Instance | Medium | Scheduler

Import (String file, Integer line)

// selected backends for generation
Backend (String name, Integer line)

Scheduler = DEFAULT() | USER_DEFINED(Integer line, Strings content)

// Definition of a new ipcore
Core (String name, String version, Integer line, Strings source, Ports ports)

Port (String name, Direction direction, Integer line, Options opts)
Direction = IN() | OUT() | DUAL()

// Instance of an ipcore
Instance(String name, String core, Integer line, Bindings bind)

Binding = Axis(String port, String axis, Integer line, Options opts)
        | CPUAxis(String port, Integer line, Options opts)

GPIO (String name, Direction direction, Integer line, Strings callback)

// Definition of the attached medium
Medium (String name, Integer line, Strings content)

// General constants and flags for board and ports (care for invalid combinations)
Option = HWQUEUE(Integer qsize)
       | SWQUEUE(Integer qsize)
       | BITWIDTH(Integer bit)
       | POLL()

// List types
Imports   * Import
Backends  * Backend
Options   * Option
Cores     * Core
GPIOs     * GPIO
Instances * Instance
Ports     * Port
Strings   * String
Bindings  * Binding
Decls     * Decl
