specification BDL

backend java {
  package de.hopp.generator.frontend
  import java.lang.String
  import java.lang.Integer
}

root BDLFile Pos

external String
external Integer

BDLFile (Imports imports, Backends backends, Options opts, Cores cores, GPIOs gpios, Instances insts, Medium medium, Scheduler scheduler)

Decl = Import | Backend | Option | Core | GPIO | Instance | Medium | Scheduler

Import (String file, Position pos)

// selected backends for generation
Backend (String name, Position pos)

Scheduler = DEFAULT() | USER_DEFINED(Position pos, Strings content)

// Definition of a new ipcore
Core (String name, String version, Position pos, Imports source, Ports ports)

Port (String name, Direction direction, Position pos, Options opts)
Direction = IN() | OUT() | DUAL()

// Instance of an ipcore
Instance(String name, String core, Position pos, Bindings bind)

Binding = Axis(String port, String axis, Position pos, Options opts)
        | CPUAxis(String port, Position pos, Options opts)

GPIO (String name, Direction direction, Position pos, Strings callback)

// Definition of the attached medium
Medium = NONE()
       | ETHERNET(MOptions opts)
       | UART(MOptions opts) 
       | PCIE(MOptions opts)

// General constants and flags for board and ports (care for invalid combinations)
Option = HWQUEUE(Integer qsize)
       | SWQUEUE(Integer qsize)
       | BITWIDTH(Integer bit)
       | POLL()

MOption = MAC  (String  val)
        | IP   (String  val)
        | MASK (String  val)
        | GATE (String  val)
        | PORTID (Integer val)

Position (String filename, Integer line)

// List types
Imports   * Import
Backends  * Backend
Options   * Option
MOptions  * MOption
Cores     * Core
GPIOs     * GPIO
Instances * Instance
Ports     * Port
Strings   * String
Bindings  * Binding
Decls     * Decl
